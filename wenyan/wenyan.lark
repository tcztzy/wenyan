
?mod: module
module: stmt*
?stmt: assign_stmt | for_in_range_stmt | expr_stmt | print_stmt
multiple_declare_expr: ("吾" | "今") "有" int TYPE ("曰" data)+
multiple_assign_stmt: multiple_declare_expr ("名之" ("曰" name_expr)+)
single_declare_expr: "有" TYPE data
single_assign_stmt: single_declare_expr ("名之" ("曰" name_expr))
?assign_stmt: multiple_assign_stmt | single_assign_stmt
expr_stmt: expr
for_in_range_stmt: "為是" (name_expr | int) "遍" stmt* "云云"
print_stmt: "書之"
?expr: constant_expr | name_expr | multiple_declare_expr | single_declare_expr
constant_expr: int | string | bool

name_expr: /「[^」]+」/
string: /「「(?!」」).*」」/
?number: int | float
int: /[零一二三四五六七八九十百千萬億兆京垓秭穣溝澗正載極]+/
float: int /又([零一二三四五六七八九][分釐毫絲忽微塵埃渺漠])+/
TYPE: "數" | "列" | "言" | "爻"
bool: "陰" | "陽"
?data: constant_expr | name_expr

%import common.WS
%ignore WS | "。" | "、"
